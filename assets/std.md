# Poi Standard Library

The Poi knowledge format is compatible with markdown text files:

- Must start with `#` (markdown title)
- A code block must use 3 backticks and `poi`
- Rules are seperated by `;`.

### Definitions

A definition uses the syntax `<sym> := <expr>`.

Definitions can be inlined under theorem proving
in Poi Reduce with `inline all`.
Under evaluation, definitions are always inlined.
See `help` for more information.

```poi
false1 := false;
not := if(false)(true);
idb := if(true)(false);
true1 := true;
and := if(if(true)(false))(false);
or := if(true)(if(true)(false));
eqb := if(if(true)(false))(if(false)(true));
xor := if(if(false)(true))(if(true)(false));
nand := if(if(false)(true))(true);
not := if(false)(if(false)(true));
exc := if(if(false)(true))(false);
imply := if(if(true)(false))(true);
fstb := if(true)(false);
sndb := if(if(true)(false))(if(true)(false));
```

### Reductions

A reduction uses the syntax `<pat> => <expr>`.

The pattern binds variables on the left side and synthesizes the expression on the right side.

There is a trade-off between reductions and equivalences.
Reductions are used to simplify automated theorem proving.

#### Generic normalization

Poi uses maximum function currying as standard form,
because there are many ways of calling functions.
This makes it easier to design rules that work in general.

Algebraic expressions, e.g. `2 + 3`, uses standard form behind the scenes.

Normalization of tuple arguments and domain constraints:

```poi
x((y, z..)) => x(y)(z);
x{(y, z..)} => x{y}{z};
```

Redistribution:

```poi
x{y}{z}(a)(b) => x{y}(a){z}(b);
(g, f)((y, z..)) => (g(y)(z), f(y)(z));
```

Concrete application and composition:

```poi
\x(_) => x;
\x Â· _ => x;
```

#### If

The `if` in Poi takes two arguments `if(a, b)`.
It returns `a` when condition is `true`,
and returns `b` when condition is `false`.

```poi
if(x)(_)(true) => x;
if(_)(x)(false) => x;
if(x)(_){_}(true) => x;
if(_)(x){_}(false) => x;
```

#### Quaterions

Quaternions are lifted to a typed vector,
in order to avoid combinatorial explosion in rules.

Conversion/lifting rules:

```poi
ð¢â‚‚ => [0, 0, 1, 0] : quat;
ð¢â‚ƒ => [0, 0, 0, 1] : quat;
(ð¢ * (x : quat)) => ([0, 1, 0, 0] * x) : quat;
((x : quat) * ð¢) => (x * [0, 1, 0, 0]) : quat;
((x * ð¢) * (y : quat)) => (x * (ð¢ * (y : quat)));

((x : quat) * ð¢) => ((x : quat) * (ð¢ : quat));
(ð¢ + (x : quat)) => ([0, 1, 0, 0] + x) : quat;
((x : quat) + ð¢) => (x + [0, 1, 0, 0]) : quat;
((x : quat) * ð¢â‚‚) => (x * [0, 0, 1, 0]) : quat;
((x : quat) * ð¢â‚ƒ) => (x * [0, 0, 0, 1]) : quat;
((x : quat) + (y * ð¢)) => (x + [0, y, 0, 0]) : quat;
(x * ð¢ + (y : quat)) => ([0, x, 0, 0] + y) : quat;
((x : quat) * (y : quat)) => (x * y) : quat;
((x : quat) + (y : quat)) => (x + y) : quat;
x + (y : quat) => (x + y) : quat;
[x, y, ð¢, z] : quat => ð¢ * ð¢â‚‚ + ([x, y, 0, z] : quat);
[x, y, z, ð¢] : quat => ð¢ * ð¢â‚ƒ + ([x, y, z, 0] : quat);
```

Quaternion algebra:

```poi
-([x, y, z, w] : quat) => [-x, -y, -z, -w] : quat;
s + ([x, y, z, w] : quat) => [s + x, s + y, s + z, s + w] : quat;
s * ([x, y, z, w] : quat) => [s * x, s * y, s * z, s * w] : quat;
([x, y, z, w] : quat) * s => [x * s, y * s, z * s, w * s] : quat;

([x0, y0, z0, w0] + [x1, y1, z1, w1]) : quat =>
 [x0+x1,y0+y1,z0+z1,w0+w1] : quat;

([x0, y0, z0, w0] * [x1, y1, z1, w1]) : quat => [
    x0*x1-y0*y1-z0*z1-w0*w1,
    x0*y1+y0*x1+z0*w1-w0*z1,
    x0*z1+z0*x1-y0*w1+w0*y1,
    x0*w1+w0*x1+y0*z1-z0*y1,
] : quat;
```

#### Types

Constants in Boolean algebra:

```poi
type_of(false) => bool;
type_of(true) => bool;
```

Unary operators in Boolean algebra (`bool -> bool`):

```poi
false1[type_of](bool) => bool;
not[type_of](bool) => bool;
idb[type_of](bool) => bool;
true1[type_of](bool) => bool;
```

Binary operators in Boolean algebra (`bool x bool -> bool`):

```poi
and[type_of](bool)(bool) => bool;
eqb[type_of](bool)(bool) => bool;
exc[type_of](bool)(bool) => bool;
false2[type_of](bool)(bool) => bool;
fstb[type_of](bool)(bool) => bool;
imply[type_of](bool)(bool) => bool;
or[type_of](bool)(bool) => bool;
nand[type_of](bool)(bool) => bool;
nor[type_of](bool)(bool) => bool;
sndb[type_of](bool)(bool) => bool;
xor[type_of](bool)(bool) => bool;
true2[type_of](bool)(bool) => bool;
```

Trigonometric operators:

```poi
acos[type_of](f64) => f64;
asin[type_of](f64) => f64;
atan[type_of](f64) => f64;
atan2[type_of](f64)(f64) => f64;
cos[type_of](f64) => f64;
exp[type_of](f64) => f64;
ln[type_of](f64) => f64;
log2[type_of](f64) => f64;
log10[type_of](f64) => f64;
sin[type_of](f64) => f64;
sqrt[type_of](f64) => f64;
tan[type_of](f64) => f64;
```

Arithmetic operators:

```poi
eq[type_of](bool)(bool) => bool;
add[type_of](f64)(f64) => f64;
sub[type_of](f64)(f64) => f64;
mul[type_of](f64)(f64) => f64;
div[type_of](f64)(f64) => f64;
rem[type_of](f64)(f64) => f64;
pow[type_of](f64)(f64) => f64;
rpow[type_of](f64)(f64) => f64;
```

List operators:

```poi
base[type_of](f64)(f64) => vec;
item[type_of](f64)(vec) => any;
len[type_of](vec) => f64;
concat[type_of](vec)(vec) => vec;
push[type_of](vec)(f64) => vec;
push_front[type_of](vec)(f64) => vec;
```

Type utilities:

```poi
type_of(\x) => compute::type_of(x);
type_of([x..]) => vec;
```

#### Symmetric normal paths

```poi
add[even] => eqb;
add[exp] => mul;
add[neg] => add;
add[odd] => xor;
and[not] => or;
add[sqrt] => sqrt Â· (add Â· ((^ 2) Â· fst, (^ 2) Â· snd));
concat[len] => add;
concat[max] => max2;
concat[min] => min2;
concat[sqnorm] => add;
concat[sum] => add;
eqb[not] => xor;
mul[even] => or;
mul[ln] => add;
mul[log10] => add;
mul[log2] => add;
mul[neg] => neg Â· mul;
mul[odd] => and;
mul_mat[det] => mul;
nand[not] => nor;
nor[not] => nand;
not[not] => not;
or[not] => and;
xor[not] => eqb;
```

#### Asymmetric normal paths

```poi
if(a)(b)[not â†’ id] => if(b)(a);
nand[not x not -> id] => and[not];
mul_mat[len â¨¯ (item(1) Â· dim) â†’ dim] => id;
```

#### Identity normal paths

```poi
x[id] => x;
id[x] => id;
```

#### Misc

```poi
inv(f) Â· f => id;
f Â· inv(f) => id;
inv(inv(f)) => f;
not Â· not => idb;
x Â· id => x;
id Â· x => x;
(fst, snd) => id;

not Â· even => odd;
not Â· odd => even;
mul{(>= 0)}{(>= 0)}[rpow{(>= 0)}(_)] => mul;

exp Â· ln => id;
ln Â· exp => id;
neg Â· neg => id;
conj Â· conj => id;
sqrt Â· sqnorm => norm;
(^ 2) Â· norm => sqnorm;
sqrt Â· (^ 2) => abs;
transpose Â· transpose => id;

false1(_) => false;
true1(_) => true;
not(\false) => true;
not(\true) => false;
id(x) => x;
and(true) => idb;
and(false) => false1;
or(true) => true1;
or(false) => idb;
fstb(x)(y) => x;
fst(x)(y) => x;
sndb(x)(y) => y;
snd(x)(y) => y;
eqb(false) => not;
eqb(true) => idb;
```

#### Complex numbers

```poi
Îµ ^ 2 => 0;
ð¢ ^ 2 => -1;
```

Complex number utilities:
```poi
Îµ * Îµ => Îµ ^ 2;

ð¢ * ð¢ => ð¢ ^ 2;
ð¢ + ð¢ => 2ð¢;
x * ð¢ + ð¢ => (x + 1) * ð¢;
ð¢ + x * ð¢ => (1 + x) * ð¢;
sqrt(-1) => ð¢;
```

#### Misc

```poi
sin(\x:int * Ï„) => sin(Ï„);
cos(\x:int * Ï„) => cos(Ï„);
tan(\x:int * Ï„) => tan(Ï„);
-(\a + \b * x) => (-a) + (-b) * x;
reci((\x + \y * ð¢)) => x / (x^2 + y^2) + (neg(y) / (x^2 + y^2)) * ð¢;
\a - \b * x => a + (-b) * x;
(\a + \b * x) - (\c + \d * x) => (a - c) + (b - d) * x;
(\a + \b * x) + (\c * x) => a + (b + c) * x;
(\a * x) + (\b * x) => (a + b) * x;
x - x => 0;
\a * x + x => (a + 1) * x;
\a * x - x => (a - 1) * x;
x + \a * x => (a + 1) * x;
x - \a * x => (1 - a) * x;
\a * (\b + \c * x) => (a * \b) + (a * c) * x;
((\a + \b * x) * (\c * x)) => ((c * x) * (a + b * x));
(\a * x) * (\b + \c * x) => ((a * c) * x^2 + (a * b) * x);
(\a + \b * x) * (\c + \d * x) => a * c + b * d * x^2 + (a * d + b * c) * x;
(\a * x) * (\b * x) => (a * b) * x^2;
\x / \y => compute::div(x, y);
(\a + \b * Îµ) / (\c + \d * Îµ) => a / c + (b * c - a * d) / c ^ 2 * Îµ;
x / (\a + \b * ð¢) => x * reci(a + b * ð¢);
not{(: vec)}([x]) => [!x];
not{(: vec)}([x, y..]) => [not(x)] ++ not{(: vec)}(y);
neg{(: vec)}(\[x]) => [-x];
neg{(: vec)}([x, y..]) => [neg(x)] ++ neg{(: vec)}(y);
sum{(: vec)}([x, y..]) => add(x)(sum{(: vec)}(y));
sum{(: vec)}(\[x]) => x;
max{(: vec)}([x, y..]) => max2(x)(max{(: vec)}(y));
max{(: vec)}(\[x]) => x;
min{(: vec)}([x, y..]) => min2(x)(min{(: vec)}(y));
min{(: vec)}(\[x]) => x;
el(x)(y)(z) => item(y)(item(x)(z));
re{(: vec)}(x) => item(0)(x);
re(a + _ * ð¢) => a;
im{(: vec)}(x) => item(1)(x);
im(_ + a * ð¢) => a;
a * ð¢ * ð¢ => a * (ð¢ * ð¢);
mulc([x0, y0])([x1, y1]) => [x0 * x1 - y0 * y1, x0 * y1 + x1 * y0];
conj([x, y]) => [x, -y];
conj(a + b * ð¢) => a + (-b) * ð¢;
```

#### Computations

```poi
-\x => compute::neg(x);
\x < \y => compute::lt(x, y);
\x <= \y => compute::le(x, y);
\x = \y => compute::eq(x, y);
\x >= \y => compute::ge(x, y);
\x > \y => compute::gt(x, y);
\x + \y => compute::add(x, y);
\x - \y => compute::sub(x, y);
\x * \y => compute::mul(x, y);
\x % \y => compute::rem(x, y);
\x ^ \y => compute::pow(x, y);
(^ \x)(\y) => compute::pow(y, x);
abs(\x) => compute::abs(x);
arity(x) => compute::arity(x);
base(\x)(\y) => compute::base(x, y);
col(\k){(: vec)}(x) => compute::col(k, x);
concat{(: vec)}(x){(: vec)}(y) => compute::concat(x, y);
dim{(: vec)}(x) => compute::dim(x);
even(\x) => compute::even(x);
inc(\x) => compute::inc(x);
item(\x)([y..]) => compute::item(x, y);
is_square_mat{(: vec)}(x) => compute::is_square_mat(x);
len{(: vec)}(x) => compute::len(x);
max2(\x)(\y) => compute::max2(x, y);
min2(\x)(\y) => compute::min2(x, y);
mul_mat{(: vec)}(x){(: vec)}(y) => compute::mul_mat(x, y);
norm(x) => sqrt(sqnorm(x));
odd(\x) => compute::odd(x);
prob(\x) => compute::prob(x);
probl(\x) => compute::probl(x);
probm(\x) => compute::probm(x);
probr(\x) => compute::probr(x);
push([x..])(y) => compute::push(x, y);
push_front([x..])(y) => compute::push_front(x, y);
range(\x)(\y)(\z) => compute::range(x, y, z);
rangel(\x)(\y)(\z) => compute::rangel(x, y, z);
rangem(\x)(\y)(\z) => compute::rangem(x, y, z);
ranger(\x)(\y)(\z) => compute::ranger(x, y, z);
reci(\x) => compute::reci(x);
sqnorm{(: vec)}(x) => sum(vec_op(mul)(x)(x));
transpose{(: vec)}(x) => compute::transpose(x);
```

Computation utilities:

```poi
\x + (\y + z) => compute::add(x, y) + z;
\x * (\y * z) => compute::mul(x, y) * z;
```

#### Misc

```poi
(* x) Â· (mul Â· (g, (* y) Â· snd)) => (* x) Â· ((* y) Â· (mul Â· (g, snd)));
(* x) Â· (mul Â· ((* y) Â· fst, g)) => (* x) Â· ((* y) Â· (mul Â· (fst, g)));
(* x) Â· (* y) => (* x * y);

-(-x) => x;
0 + x => x;
x + 0 => x;
0 - x => -x;
x - 0 => x;
1 * x => x;
x * 1 => x;
(* 0) => 0;
_ * 0 => 0;
\x / âˆž => 0;
x ^ 1 => x;
x ^ 0 => 1;

x * (-y) => (-x) * y;
```

#### Concreteness

Concreteness is used to in meta-reasoning about whether an expression
will return a concrete value.

```poi
(x : \) + (y : \) => (x + y) : \;
(x : \) - (y : \) => (x - y) : \;
(x : \) * (y : \) => (x * y) : \;
(x : \) / (y : \) => (x / y) : \;
(x : \) % (y : \) => (x % y) : \;
(x : \) ^ (y : \) => (x ^ y) : \;
(^ x : \)(y : \) => (^ x)(y) : \;
```

#### Vector operations

```poi
and{(: vec)}(x){(: vec)}(y) => vec_op(and)(x)(y);
or{(: vec)}(x){(: vec)}(y) => vec_op(or)(x)(y);
add{(: vec)}(x){(: vec)}(y) => vec_op(add)(x)(y);
sub{(: vec)}(x){(: vec)}(y) => vec_op(sub)(x)(y);
mul{(: vec)}(x){(: vec)}(y) => vec_op(mul)(x)(y);
div{(: vec)}(x){(: vec)}(y) => vec_op(div)(x)(y);
rem{(: vec)}(x){(: vec)}(y) => vec_op(rem)(x)(y);
pow{(: vec)}(x){(: vec)}(y) => vec_op(pow)(x)(y);
rpow{(: vec)}(x){(: vec)}(y) => vec_op(rpow)(x)(y);
```

Vector operation reductions:

```poi
vec_op(f)([x0, y0..])([x1, y1..]) => concat([f(x0)(x1)])(vec_op(f)(y0)(y1));
vec_op(f)(\[x])(\[y]) => [f(x)(y)];
vec_uop(f)([x, y..]) => concat([f(x)])(vec_uop(f)(y));
vec_uop(f)(\[x]) => [f(x)];
```

#### Misc

```poi
dot{(: vec)}([x0, y0]){(: vec)}([x1, y1]) => x0 * x1 + y0 * y1;
not Â· (not Â· x) => x;
```

#### Ranges

```poi
and Â· (le, ge) => eq;
and Â· (f, f) => f;
and Â· ((>= \x), (>= \y)) => (>= max2(x)(y));
and Â· ((> \x), (> \y)) => (> max2(x)(y));
and Â· ((<= \x), (<= \y)) => (<= min2(x)(y));
and Â· ((< \x), (< \y)) => (< min2(x)(y));
and Â· ((< x), (<= x)) => (< x);
and Â· ((<= x), (< y)) => and Â· ((< y), (<= x));
and Â· ((< x), (> x)) => false;
and Â· ((> x), (< y)) => and Â· ((< y), (> x));
and Â· ((> x), (<= y)) => and Â· ((<= y), (> x));
and Â· ((<= x), (>= x)) => (= x);
and Â· ((> x), (>= x)) => (> x);
and Â· ((>= x), (> y)) => and Â· ((> y), (>= x));
and Â· ((>= x), (< y)) => and Â· ((< y), (>= x));
and Â· ((>= x), (<= y)) => and Â· ((<= y), (>= x));
and Â· ((< \x), (> \y)) => if(false)(rangem(min2(x)(y))(max2(x)(y)))(x <= y);
and Â· ((< \x), (>= \y)) => if(false)(rangel(min2(x)(y))(max2(x)(y)))(x <= y);
and Â· ((<= \x), (> \y)) => if(false)(ranger(min2(x)(y))(max2(x)(y)))(x <= y);
and Â· ((<= \x), (>= \y)) => if(false)(range(min2(x)(y))(max2(x)(y)))(x < y);
and Â· ((< \x), (<= \y)) => if((< x))((<= y))(x <= y);
and Â· ((> \x), (>= \y)) => if((> x))((>= y))(x >= y);
```

```poi
or Â· ((< x), (<= x)) => (<= x);
or Â· ((<= x), (< y)) => or Â· ((< y), (<= x));
or Â· ((< x), (= x)) => (<= x);
or Â· ((= x), (> x)) => (>= x);
or Â· ((< x), (>= x)) => true;
or Â· ((<= x), (> x)) => true;
or Â· ((<= x), (>= x)) => true;
or Â· ((>= x), (< y)) => or Â· ((< y), (>= x));
or Â· ((> x), (<= y)) => or Â· ((<= y), (> x));
or Â· ((>= x), (<= y)) => or Â· ((<= y), (>= x));
or Â· ((= y), (< x)) => or Â· ((< x), (= y));
or Â· ((> x), (= y)) => or Â· ((= y), (> x));
or Â· ((= x), (>= x)) => (>= x);
or Â· ((<= x), (= x)) => (<= x);
or Â· ((>= x), (= y)) => or Â· ((= y), (>= x));
or Â· ((= x), (<= y)) => or Â· ((<= y), (= x));
or Â· ((> x), (>= x)) => (>= x);
or Â· ((>= x), (> y)) => or Â· ((> y), (>= x));
or Â· ((< \x), (<= \y)) => if((<= y))((< x))(x <= y);
or Â· ((> \x), (>= \y)) => if((>= y))(gt(x))(x >= y);
or Â· (f, f) => f;
```

```poi
not Â· (< x) => (>= x);
not Â· (<= x) => (> x);
not Â· (>= x) => (< x);
not Â· (> x) => (<= x);
```

#### Derivatives

You can use `deriv` or `ð` (unicode).

```poi
ð(!\x)(x) => 1;
ð(!\x)(\y) => 0;
ð(!\x)(y:\) => 0;
ð(!\x)(\k * y) => k * ð(x)(y);
ð(!\x)(k:\ * y) => k:\ * ð(x)(y);
ð(!\x)(Ï€ * y) => Ï€ * ð(x)(y);
ð(!\x)(Ï„ * y) => Ï„ * ð(x)(y);
ð(!\x)(x ^ \k) => k * x ^ (k - 1);
ð(!\x)(sin(x)) => cos(x);
ð(!\x)(cos(x)) => -sin(x);
ð(!\x)(sin(\k * x)) => k * cos(k * x);
ð(!\x)(cos(\k * x)) => -k * sin(k * x);
ð(!\x)(exp(x)) => exp(x);
ð(!\x)(exp(\k * x)) => k * exp(k * x);
ð(!\x)(y + z) => ð(x)(y) + ð(x)(z);
ð(!\x)(y - z) => ð(x)(y) - ð(x)(z);
```

Constant derivative simplification:

```poi
ð(x)((a : \) ^ \b) => 0;
```

Derivatives utilities:

```poi
ð(!\x)(\k * m:\ * y) => k * m:\ * ð(x)(y);
ð(!\x)((x - s:\)^2) => 2 * (x - s:\);
```

#### Indefinite integrals

You can use `integ` or `âˆ«` (unicode).

```poi
âˆ«(!\x)(c)(x) => c + 0.5 * x ^ 2;
âˆ«(!\x)(c)(\k) => c + k * x;
âˆ«(!\x)(c)(\k * y) => k * âˆ«(x)(c / k)(y);
âˆ«(!\x)(c)(x ^ \k) => c + 1 / (k + 1) * x ^ (k + 1);
âˆ«(!\x)(c)(cos(x)) => c + sin(x);
âˆ«(!\x)(c)(sin(x)) => c + -cos(x);
âˆ«(!\x)(c)(exp(x)) => c + exp(x);
âˆ«(!\x)(c)(exp(\k * x)) => c + 1 / k * exp(k * x);
âˆ«(!\x)(c)(d(x)(y) * exp(y)) => c + exp(y);
âˆ«(!\x)(c)(\k ^ x) => c + k ^ x / ln(k);
âˆ«(!\x)(c)(ln(x)) => c + (-x + x * ln(x));
```

Indefinite integral utilities:
```poi
(\k * ((c / \k) + y)) => c + k * y;
```

### Partial derivatives

You can use `pariv * x` or `âˆ‚x` (unicode).

Notice that there is an ambiguity in the standard notation,
which is fixed by using the following notation:

- `âˆ‚(y) / âˆ‚x` means taking the partial derivative of `y` with respect to `x`
- `âˆ‚y / âˆ‚x` means the change of `y` with respect to `x`

```poi
âˆ‚(a + b) / âˆ‚c => âˆ‚(a) / âˆ‚c + âˆ‚(b) / âˆ‚c;
âˆ‚(a - b) / âˆ‚c => âˆ‚(a) / âˆ‚c - âˆ‚(b) / âˆ‚c;
âˆ‚(x) / âˆ‚x => ð(x)(x);
âˆ‚(x^\k) / âˆ‚x => ð(x)(x^k);
âˆ‚((x - s)^\k) / (âˆ‚ * x!>s) => k * (x - s) ^ (k - 1);
âˆ‚(a * b) / (âˆ‚ * x!>a) => a * (âˆ‚(b) / âˆ‚x);
âˆ‚(x) / (âˆ‚ * y!>x) => ð(y)(x:\);
âˆ‚((âˆ‚x / âˆ‚t) ^ 2) / (âˆ‚ * x!>t) => 0;
```

#### Equality domain constraints

```poi
and{eq} => fstb;
or{eq} => fstb;
eq{eq} => true;
add{eq}(x)(_) => 2 * x;
mul{eq}(x)(_) => x ^ 2;
sub{eq} => 0;
```

Equality constraint utilities:
```poi
\x{eq}(_) => x;
eq{(: vec)}(x){(: vec)}(x) => eq{eq}(x)(x);
```

#### Misc

```poi
div{and Â· (eq, (> 0) Â· fst)} => 1;
f{true2} => f;
f{true1} => f;
(x^\k * x) => x^(k + 1);
(x * x^\k) => x^(k + 1);
(x^\a * x^\b) => x^(a + b);
(x * x) => x^2;
```

#### Trivial paths (domains)

You can type `triv` instead of `âˆ€`.
For more information, see `help triv`.

```poi
âˆ€(f:[arity]2{g0}{g1}) => (g0, g1);
âˆ€(f:[arity]1{g}) => g;
âˆ€(f:!{}) => true;
```

#### Existential paths (codomains)

You can type `ex` instead of `âˆƒ`.
For more information, see `help ex`.

Generic laws:

```poi
âˆƒ(\x) => eq(x);
âˆƒ(f{f}) => idb;
```

```poi
âˆƒ(false1) => not;
âˆƒ(not) => true1;
âˆƒ(idb) => true1;
âˆƒ(true1) => idb;
âˆƒ(and) => true1;
âˆƒ(or) => true1;
âˆƒ(nand) => true1;
âˆƒ(nor) => true1;
âˆƒ(xor) => true1;
âˆƒ(eqb) => true1;
âˆƒ(exc) => true1;
âˆƒ(imply) => true1;
âˆƒ(fstb) => true1;
âˆƒ(sndb) => true1;
âˆƒ(id) => true;

âˆƒ(add{(>= x)}{(>= y)}) => (>= x + y);
âˆƒ(add{(> x)}{(> y)}) => (> x + y);
âˆƒ(add{(<= x)}{(<= y)}) => (<= x + y);
âˆƒ(add{(< x)}{(< y)}) => (< x + y);
```

#### Constants

```poi
!\a + \b => b + a;
!\a + b:\ => b:\ + a;
!\a * \b => b * a;
!\a * b:\ => b:\ * a;
!\a + (\b + c) => b + (a + c);
!\a + (b:\ + c) => b:\ + (a + c);
\a + !\b - !\c => a + (b - c);
a:\ + !\b - !\c => a:\ + (b - c);
```

Constants utilities:

```poi
\a * b * (\c * d) => (a * c) * b * d;
```

#### Misc

```poi
idb => id;
fstb => fst;
sndb => snd;
eqb => eq;

len Â· concat => concat[len] Â· (len Â· fst, len Â· snd);
sum Â· concat => concat[sum] Â· (sum Â· fst, sum Â· snd);
max Â· concat => concat[max] Â· (max Â· fst, max Â· snd);
min Â· concat => concat[min] Â· (min Â· fst, min Â· snd);
sqnorm Â· concat => concat[sqnorm] Â· (sqnorm Â· fst, sqnorm Â· snd);
norm Â· concat => sqrt Â· (sqnorm Â· concat);
len Â· base(x) => x;
item(0) Â· dim => len;
(f Â· fst){x}(a){_}(_) => f{x}(a);
(f Â· fst)(a)(_) => f(a);
(f Â· snd){_}(_){x}(a) => f{x}(a);
(f Â· snd)(_)(a) => f(a);
(f Â· fst){_}(a)(_) => f(a);
(f Â· snd){_}(_)(a) => f(a);
(f Â· fst) Â· (x, _) => f Â· x;
(f Â· snd) Â· (_, x) => f Â· x;

(x, y) Â· (a, b) => (x Â· (a, b), y Â· (a, b));
(x, y, z) Â· (a, b, c) => (x Â· (a, b, c), y Â· (a, b, c), z Â· (a, b, c));

cos(x) ^ 2 + sin(x) ^ 2 => 1;

f:!{}([x..]) => f{(: vec)}(x);

(-(x + y)) => (-x + -y);
(-(x * y)) => (-x * y);
(x + x) => (2 * x);
```

### Equivalences

Equivalences are used to present available choices after reduction.

An equivalence uses the syntax `<expr> <=> <expr>`.
When `<=>>` is used, it means reduction from left to right under evaluation.

Equivalences are similar to reductions, but with the difference
that they might work both ways.
Some equivalences have fewer variables or uses `compute::` on one side,
which means they can only be used from left to right.

#### Trigonometry

```poi
acos(\x) <=>> compute::acos(x);
asin(\x) <=>> compute::asin(x);
atan(\x) <=>> compute::atan(x);
atan2(\x)(\y) <=>> compute::atan2(x, y);
cos(\x) <=>> compute::cos(x);
exp(\x) <=>> compute::exp(x);
ln(\x) <=>> compute::ln(x);
log2(\x) <=>> compute::log2(x);
log10(\x) <=>> compute::log10(x);
sin(\x) <=>> compute::sin(x);
sqrt(\x:(< 0)) <=>> mul(sqrt(x))(ð¢);
sqrt(\x:(>= 0)) <=> compute::sqrt(x);
tan(\x) <=>> compute::tan(x);
```

Constants:
```poi
pi <=>> 3.141592653589793;
tau <=>> 6.283185307179586;
```

Trigonometry utilities:
```poi
2Ï€ <=> Ï„;
cos(Ï„) => 1;
sin(Ï„) => 0;
sqrt(1) => 1;
sqrt(x) ^ 2 => x;
tan(Ï„) => 0;
```

#### Ranges to probabilities

```poi
range(0)(1) <=> prob;
rangel(0)(1) <=> probl;
ranger(0)(1) <=> probr;
rangem(0)(1) <=> probm;
```

#### Misc

```poi
x ^ 2 <=> x * x;

not Â· nand <=> and;
not Â· nor <=> or;
not Â· and <=> nand;
not Â· or <=> nor;
not Â· eqb <=> xor;
not Â· xor <=> eqb;
(>= x) <=> le(x);
(> x) <=> lt(x);
(<= x) <=> ge(x);
(< x) <=> gt(x);
and Â· (f, g) <=> and Â· (g, f);
or Â· (f, g) <=> or Â· (g, f);
nand Â· (f, g) <=> nand Â· (g, f);
nor Â· (f, g) <=> nor Â· (g, f);
(not Â· and) Â· (not Â· fst, not Â· snd) <=> or;

el(x)(y) <=> item(x) Â· item(y);

a - (b - c) <=> a - b + c;
a - b - c <=> a - (b + c);
((a - b) * c) <=> (a * c - b * c);
((a + b) * c) <=> (a * c + b * c);
((a + b) - c) <=> (a + (b - c));

((a + b)^2) <=> (a^2 + 2 * a * b + b^2);
((a - b)^2) <=> (a^2 - 2 * a * b + b^2);
((a + b) * (a - b)) <=> (a^2 - b^2);
((a * b)^2) <=> (a^2 * b^2);
((a / b)^2) <=> (a^2 / b^2);
(a / b / b) <=> (a / b^2);
(^ a)(b) <=> (b ^ a);
```

#### Distributivity

```poi
a * (b + c) <=> a * b + a * c;
a * (b - c) <=> a * b - a * c;
```

#### Associativity

```poi
a + (b + c) <=> a + b + c;
a * (b * c) <=> a * b * c;
```

#### Commutativity

```poi
a & b <=> b & a;
a | b <=> b | a;
(a = b) <=> (b = a);
a + b <=> b + a;
a * b <=> b * a;
nand(a)(b) <=> nand(b)(a);
nor(a)(b) <=> nor(b)(a);
xor(a)(b) <=> xor(b)(a);
```

Commutativity utilities:

```poi
x0 + x1 + x2 + x3 <=> x3 + x0 + x1 + x2;
x0 + x1 + x2 + x3 <=> x0 + x3 + x2 + x1;
x0 + x1 + x2 + x3 + x4 + x5 + x6 + x7 <=>
x7 + x0 + x1 + x2 + x3 + x4 + x5 + x6;
x0 + x1 + x2 + x3 + x4 + x5 + x6 + x7 <=>
x0 + x7 + x6 + x5 + x4 + x3 + x2 + x1;
x0 + x1 + x2 + x3 + x4 + x5 + x6 + x7 + x8 + x9 + x10 + x11 <=>
x11 + x0 + x1 + x2 + x3 + x4 + x5 + x6 + x7 + x8 + x9 + x10;
x0 + x1 + x2 + x3 + x4 + x5 + x6 + x7 + x8 + x9 + x10 + x11 <=>
x0 + x11 + x10 + x9 + x8 + x7 + x6 + x5 + x4 + x3 + x2 + x1;
```

#### Function inverses

```poi
inv(id) <=> id;
inv(not) <=> not;
```

#### Algebra misc

```poi
inc(x) <=> 1 + x;
(x + -y) <=> (x - y);
neg(x + y) <=> neg(x) - y;
neg(x) <=> -1 * x;
(a + b = c) <=> ((-a + a) + b = -a + c);
(a - b = c) <=> (a - (b - b) = c + b);
(a + b = c) <=> (b = c - a);
(a + b = c) <=> (a = c - b);
(a + b = c + b) <=> (a = c);
mul(-1) <=> neg;
(a - b = c - b) <=> (a = c);
(a / b = c / b) <=> (a * (b / b) = c * (b / b));
(a / c + b / c) <=> ((a + b) / c);
((a * b) / c) <=> (a * (b / c));
(a * b - a) <=> (a * (b - 1));
(- y)(x) <=> (x - y);
(x / y) <=> x * reci(y);
(/ y)(x) <=> (x / y);
```

#### Logarithms and exponentials

```poi
log10(x) <=> ln(x) / ln(10);
log2(x) <=> ln(x) / ln(2);
ln(y ^ x) <=> x * ln(y);
log2(y ^ x) <=> x * log2(y);
log10(y ^ x) <=> x * log10(y);
ln(x * y) <=> ln(x) + ln(y);
log10(x * y) <=> log10(x) + log10(y);
log2(x * y) <=> log2(x) + log2(y);
exp(x + y) <=> exp(x) * exp(y);
exp(x * ln(y)) <=> y^x;
```

#### Generic transformations
```poi
(f:[arity]1 Â· g) <=> f:[arity]1[inv(g) -> id];
(f Â· (g0 Â· fst, g1 Â· snd)) <=> f[inv(g0) x inv(g1) -> id];
(f Â· inv(g)) <=> f[g -> id];
(f Â· (inv(g0) Â· fst, inv(g1) Â· snd)) <=> f[g0 x g1 -> id];
f[id -> g2] Â· inv(g1) <=> f[g1 -> g2];
f[id -> g2] Â· g1 <=> f[inv(g1) -> g2];
inv(f) <=> id[f -> id];
inv(f Â· g) <=> inv(g) Â· inv(f);
id[(f Â· g) -> id] <=> id[f -> id[g -> id]];
((g2 Â· f Â· g1) = h) <=> (f = (inv(g2) Â· h Â· inv(g1)));
((f Â· g) = h) <=> (((inv(f) Â· f) Â· g) = (inv(f) Â· h));
((f Â· g) = h) <=> ((f Â· (g Â· inv(g))) = (h Â· inv(g)));
(f[g] = h) <=> (f = h[inv(g)]);
(f[g1 -> g2] = h) <=> (f = h[inv(g1) -> inv(g2)]);
(f[g0 x g1 -> g2] = h) <=> (f = h[inv(g0) x inv(g1) -> inv(g2)]);
(f[g1 -> id] = h[id -> inv(g2)]) <=> (f[g1 -> g2] = h);
h Â· f[g -> id] <=> f[g -> h];
f[id -> g] <=> g Â· f;
f[id x id -> g] <=> g Â· f;
f:!{}(a)(a) <=> f{eq}(a)(a);
f:!{} Â· (g, g) <=> f{eq} Â· (g, g);
f[g][h] <=> f[h Â· g];
f[g0 -> g1][g2 -> g3] <=> f[(g2 Â· g0) -> (g3 Â· g1)];
f[g0 x g1 -> g2][h0 x h1 -> h2] <=> f[(h0 Â· g0) x (h1 Â· g1) -> (h2 Â· g2)];
f Â· (g Â· (h0, h1)) <=> (f Â· g) Â· (h0, h1);
(f Â· (g0, g1)) Â· (h0, h1) <=> f Â· ((g0, g1) Â· (h0, h1));
f Â· (g Â· h) <=> (f Â· g) Â· h;
f:[arity]1[g] <=> f:[arity]1[g -> id][id -> g];
(f Â· (g0, g1)){x}(a){y}(b) <=> f(g0{x}(a){y}(b))(g1{x}(a){y}(b));
(f Â· (g0, g1))(a)(b) <=> f(g0(a)(b))(g1(a)(b));
(f Â· (g0, g1))(a) <=> f(g0(a))(g1(a));
(f Â· (g0(a), g1(b)))(c) <=> (f Â· (g0 Â· fst, g1 Â· snd))(a)(b)(c);
(f Â· g:[arity]2)(a)(b) <=> f(g:[arity]2(a)(b));
(f Â· g:[arity]2){x}(a){y}(b) <=> f(g:[arity]2{x}(a){y}(b));
(f Â· g:[arity]1)(a) <=> f(g:[arity]1(a));
(f Â· g:[arity]2){x}(a){y}(b) <=> (f Â· g:[arity]2{x}{y})(a)(b);
(f Â· g:[arity]1){x}(a) <=> f(g:[arity]1{x}(a));
(g Â· f:[arity]2){_}(a){_}(b) <=> f:[arity]2[g](g(a))(g(b));
(g Â· f:[arity]1){_}(a) <=> f:[arity]1[g](g(a));
(g Â· f:[arity]2)(a)(b) <=> f:[arity]2[g](g(a))(g(b));
g Â· f:[arity]2 <=> f:[arity]2[g] Â· (g Â· fst, g Â· snd);
(g Â· f:[arity]1)(a) <=> f:[arity]1[g](g(a));
(g, f)(a) <=> (g(a), f(a));
f Â· (g0, g1)(a) <=> (f Â· (g0, g1))(a);
(g Â· f){h} <=> g Â· f{h};
f[g0 x g1 -> g2] Â· (g0 Â· fst, g1 Â· snd) <=> g2 Â· f;
f[g0 -> g1] Â· g0 <=> g1 Â· f;
f[id x g0 -> g1] Â· (fst, g0 Â· snd) <=> g1 Â· f;
f[g0 x id -> g1] Â· (g0 Â· fst, snd) <=> g1 Â· f;
f[g -> g] <=> f[g];
f[g x g -> g] <=> f[g];
(f(x) = g(x)) <=> (f = g);
g2 Â· f[g0 x g1 -> id] <=> (g2 Â· f)[g0 x g1 -> id];
h Â· f[g0 x g1 -> id] <=> f[g0 x g1 -> h];
```
